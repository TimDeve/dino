(load "./interfaces.carp")

(sig circ-rect-get-y-distance (Fn [(Ref a) (Ref b)] Int))
(defn circ-rect-get-y-distance [circ rect]
 (let [circ-y-pos (y-pos circ)
       rect-y-pos  (y-pos rect)
       val-rect-height (rect-height rect)]
  (if (< circ-y-pos rect-y-pos)
      rect-y-pos
      (if (> circ-y-pos (+ val-rect-height rect-y-pos))
          (+ val-rect-height rect-y-pos)
          circ-y-pos))))

(sig circ-rect-get-x-distance (Fn [(Ref a) (Ref b)] Int))
(defn circ-rect-get-x-distance [circ rect]
 (let [circ-x-pos (x-pos circ)
       rect-x-pos  (x-pos rect)
       val-rect-width (rect-width rect)]
  (if (< circ-x-pos rect-x-pos)
      rect-x-pos
      (if (> circ-x-pos (+ val-rect-width rect-x-pos))
          (+ val-rect-width rect-x-pos)
          circ-x-pos))))

(sig circ-rect-colliding? (Fn [(Ref a) (Ref b)] Bool))
(defn circ-rect-colliding? [circ rect]
  (let [dist-x (- (x-pos circ) (circ-rect-get-x-distance circ rect))
        dist-y (- (y-pos circ) (circ-rect-get-y-distance circ rect))
        distance (Math.sqrt (+ (* dist-x dist-x) (* dist-y dist-y)))]
   (<= distance (radius circ))))

(sig rect-colliding? (Fn [(Ref a) (Ref b)] Bool))
(defn rect-colliding? [rect-one rect-two]
  (and
   (< (x-pos rect-one) (+ (x-pos rect-two) (rect-width rect-two)))
   (and
    (> (+ (x-pos rect-one) (rect-width rect-one)) (x-pos rect-two))
    (and
     (< (y-pos rect-one) (+ (y-pos rect-two) (rect-height rect-two)))
     (> (+ (y-pos rect-one) (rect-height rect-one)) (y-pos rect-two))))))

(sig circ-colliding? (Fn [(Ref a) (Ref b)] Bool))
(defn circ-colliding? [circ-one circ-two]
  (let [dx (- (x-pos circ-one) (x-pos circ-two))
        dy (- (y-pos circ-one) (y-pos circ-two))
        distance (Math.sqrt (+ (* dx dx) (* dy dy)))]
    (< distance (+ (radius circ-one) (radius circ-two)))))

