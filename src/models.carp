(def floor-y 120)

(deftype Obstacle [x Int jumped Bool])
(defmodule Obstacle
 (defn height [] 16)
 (annotate height "inline")
 (defn width [] 16)
 (annotate width "inline")
 (defn y [] (- floor-y (height)))
 (annotate y "inline")

 (defn hitbox-x-pos [obs] @(x obs))
 (implements x-pos Obstacle.hitbox-x-pos)
 (sig hitbox-y-pos (Fn [(Ref Obstacle)] Int))
 (defn hitbox-y-pos [obs] (y))
 (implements y-pos Obstacle.hitbox-y-pos)
 (sig hitbox-width (Fn [(Ref Obstacle)] Int))
 (defn hitbox-width [obs] (width))
 (implements rect-width Obstacle.hitbox-width)
 (sig hitbox-height (Fn [(Ref Obstacle)] Int))
 (defn hitbox-height [obs] (height))
 (implements rect-height Obstacle.hitbox-height))

(deftype PlayerState (Running [Int]) Jumping Falling)
(deftype Player [x Int y Int state PlayerState])
(defmodule Player
 (defn height [] 32)
 (annotate height "inline")
 (defn width [] 16)
 (annotate width "inline")
 (defn new [] (Player.init 20 (- @&floor-y (height)) (PlayerState.Running 1)))

 (defn tick [player]
  (update-state player &(fn [state]
                         (match state
                           (PlayerState.Running tik)
                           (PlayerState.Running (if (>= tik 30) 1 (inc tik)))
                           _             state))))

 (defn hitbox-x-pos [player] @(x player))
 (implements x-pos Player.hitbox-x-pos)
 (defn hitbox-y-pos [player] @(y player))
 (implements y-pos Player.hitbox-y-pos)
 (sig hitbox-width (Fn [(Ref Player)] Int))
 (defn hitbox-width [player] (width))
 (implements rect-width Player.hitbox-width)
 (sig hitbox-height (Fn [(Ref Player)] Int))
 (defn hitbox-height [player] (height))
 (implements rect-height Player.hitbox-height))

(deftype WorldState (Intro [Int]) Playing Paused Dead)
(deftype World [p Player
                obstacles (Array Obstacle)
                state WorldState
                score Int
                back-zero-x Int
                back-one-x Int])

(defmodule World
 (defn tick [world]
  (update-state world
                &(fn [state]
                  (match state
                    (WorldState.Intro tik) (WorldState.Intro (inc tik))
                    _                      state))))

 (defn new []
  (World.init (Player.new)
              [(Obstacle.init Video.screen-width false)
               (Obstacle.init (* 2 Video.screen-width) false)]
              (WorldState.Intro 0)
              0
              0
              0)))

