(use-all Object Assets Debug)

(defn make-bin []
  (init (bit-or attr0-square attr0-8bpp)
        attr1-size-16
        (attr2-build (bin-id) (pal-id) 0)))

(defn make-witch []
  (init (bit-or attr0-square attr0-8bpp)
        attr1-size-16
        (attr2-build (witch-id) (pal-id) 0)))

(defn make-unicorn [frame]
  (init (bit-or attr0-tall attr0-8bpp)
        attr1-size-16-32
        (case frame
         0 (attr2-build (unicorn-id) (pal-id) 0)
         1 (attr2-build (+ (unicorn-id) 16) (pal-id) 0)
         2 (attr2-build (+ (unicorn-id) 32) (pal-id) 0)
         3 (attr2-build (+ (unicorn-id) 48) (pal-id) 0)
         (halt "There isn't a frame for this"))))

(defn make-obs [obs]
  (match-ref (Obstacle.ty obs)
    (ObstacleType.Low) (make-bin)
    (ObstacleType.High) (make-witch)))

(defn render-obstacles [obstacles]
  (foreach [obstacle obstacles]
   (with Obstacle
    (when (< @(x obstacle) (- Video.screen-width (width)))
      (buffer-push (set-pos (make-obs obstacle) @(x obstacle) (y obstacle)))))))

(defn render-player [player]
  (with Player
   (do
    (buffer-push
      (set-pos (match-ref (state player)
                  PlayerState.Jumping (make-unicorn 2)
                  PlayerState.Dodging (make-unicorn 3)
                  (PlayerState.Running tik) (if (>= tik &15) (make-unicorn 0) (make-unicorn 1))
                  _                   (make-unicorn 0))
               @(x player)
               @(y player))))))

(defn-do render-background [world]
  (Background.control-x-offset (Background.Zero) @(World.back-zero-x world))
  (Background.control-x-offset (Background.One) @(World.back-one-x world)))

(defn-do render-score [world]
  (Text.set-pos 0 0)
  (Text.erase-line)
  (ignore (Text.write-fmt "Score:%i" (World.score world))))

(defn render-playing-game [world]
  (do
   (render-background world)
   (render-score world)
   (render-obstacles (World.obstacles world))
   (render-player (World.p world))))

(defn-do render-paused-game [world]
  (render-playing-game world)
  (Text.set-pos 100 0)
  (ignore (Text.write "Paused")))

(defn-do render-start-screen []
  (Text.erase-line)
  (Text.set-pos 70 0)
  (ignore (Text.write "Press start...")))

(defn-do render-death-screen []
  (Text.set-pos 70 0)
  (ignore (Text.write "Start to try again.")))

(sig render (Fn [(Ref World)] ()))
(defn render [world]
  (do
    (match-ref (World.state world)
      WorldState.Playing   (render-playing-game world)
      WorldState.Paused    (render-paused-game world)
      (WorldState.Intro _) (render-start-screen)
      WorldState.Dead      (render-death-screen))
    (Object.buffer-blit)))
